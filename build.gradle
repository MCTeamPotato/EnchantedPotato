plugins {
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
            "enchantedpotato.mixins.json"
        ]
    }
    mixin.defaultRefmapName.set("enchantedpotato.refmap.json")
}

repositories {
    maven { url "https://maven.parchmentmc.org" }
    maven { url "https://maven.blamejared.com/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://maven.minecraftforge.net/" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    modCompileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }

    forge "net.minecraftforge:forge:${forge_version}"
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)
}

jar {
    manifest {
        attributes([
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}